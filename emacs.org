#+TITLE: GNU/Emacs
#+AUTHOR: Amlesh Sivanantham (zaml)
#+ROAM_ALIAS:
#+ROAM_KEY: https://www.gnu.org/software/emacs/
#+ROAM_TAGS: CONFIG SOFTWARE EMACS
#+CREATED: [2021-03-27 Sat 00:17]
#+LAST_MODIFIED: [2021-04-20 Tue 13:20:01]
#+STARTUP: overview

#+DOWNLOADED: screenshot @ 2021-04-07 17:14:36
[[file:data/xkcd_378.png]]

/[[https://xkcd.com/378/][Real Programmers - Randall Munroe]]/

* Useful Resources

There are some cool configurations from other people that I've taken influence from for my own emacs configuration.

- [[https://config.daviwil.com/emacs][David Wilson's (System Crafters) Emacs Configuration]]
- [[https://pages.sachachua.com/.emacs.d/][Sasha Chau's Emacs Configuration]]
- [[https://tecosaur.github.io/emacs-config/config.html][Tecosaur's Doom Emacs Configuration]]
- [[https://config.phundrak.com/emacs][Phundrak's Emacs Configuration]]
- [[https://writequit.org/org/][WriteQuit's Dotfiles with Org]]
- [[https://protesilaos.com/dotemacs/][Protosilaos Stavrou's Dotfiles]]

* Tips and Tricks
As I find things to write here, I'll put it in.

* Emacs Configuration
:PROPERTIES:
:header-args:emacs-lisp: :tangle ~/.config/emacs/init.el :comments both :mkdirp yes
:END:
** Garbage Collector Hooks

Supposedly makes the startup a bit more effecient. We also revert the changes to the GC via a hook once the startup has completed.

#+begin_src emacs-lisp
(defvar file-name-handler-alist-original file-name-handler-alist)

(setq gc-cons-threshold most-positive-fixnum
      gc-cons-percentage 0.6
      file-name-handler-alist nil
      site-run-file nil)

(defvar zamlz/gc-cons-threshold 100000000)

(add-hook 'emacs-startup-hook ; hook run after loading init files
          (lambda ()
            (setq gc-cons-threshold zamlz/gc-cons-threshold
                  gc-cons-percentage 0.1
                  file-name-handler-alist file-name-handler-alist-original)))

(add-hook 'minibuffer-setup-hook
          (lambda ()
            (setq gc-cons-threshold (* zamlz/gc-cons-threshold 2))))
(add-hook 'minibuffer-exit-hook
          (lambda ()
            (garbage-collect)
            (setq gc-cons-threshold zamlz/gc-cons-threshold)))
#+end_src

** Update Load Path

Optimize: Force "lisp"" and "site-lisp" at the head to reduce the startup time.

#+begin_src emacs-lisp
(defun update-load-path (&rest _)
  "Update `load-path'."
  (dolist (dir '("site-lisp" "lisp"))
    (push (expand-file-name dir user-emacs-directory) load-path)))

(defun add-subdirs-to-load-path (&rest _)
  "Add subdirectories to `load-path'."
  (let ((default-directory (expand-file-name "site-lisp" user-emacs-directory)))
    (normal-top-level-add-subdirs-to-load-path)))

(advice-add #'package-initialize :after #'update-load-path)
(advice-add #'package-initialize :after #'add-subdirs-to-load-path)

(update-load-path)
#+end_src

The =site-lisp= directory may not be populated. /(Do I even need this?)/

#+begin_src conf :tangle ~/.config/emacs/site-lisp/.keep :mkdirp yes
#placeholder file to generate directory
#+end_src

** Internal

#+begin_src emacs-lisp
(require 'init-package)
(require 'init-dired)
(require 'init-ibuffer)
#+end_src

*** Reorganize This
**** Basic Emacs Setup

Lets get some basic settings out of the way here.

#+begin_src emacs-lisp
(use-package emacs
  :preface
  ;; Setup personal preferances
  (defvar zamlz/indent-width 4)   ; tab size
  (defvar zamlz/default-screen-width 100)
  :custom
  ;; Configure personal information
  (user-full-name "Amlesh Sivanantham")
  (user-mail-address "zamlz@pm.me")
  (user-login-name "zamlz")
  ;; Other basic settings
  (ring-bell-function 'ignore) ; minimise distractio
  (frame-resize-pixelwise t)
  (default-directory "~/")
  :config
  ;; Set Environment Variables
  (setenv "PINENTRY_USER_DATA" "rofi")
  (setenv "VISUAL" "emacsclient --socket-name=xorg-emacs-daemon" )
  (setenv "EDITOR" (getenv "VISUAL"))
  ;; Configure Specific UI changes
  (tool-bar-mode -1)          ; Disable the toolbar
  (menu-bar-mode -1)          ; disable the menubar
  (set-fringe-mode 10)        ; Give some breathing room
  (blink-cursor-mode 1)       ; Let the cursor be blinking
  (semantic-mode 1)
  ;; (tooltip-mode -1)           ; Disable tooltips
  ;; Always use spaces for indentation
  (setq-default indent-tabs-mode nil
                tab-width zamlz/indent-width
                fill-column zamlz/default-screen-width))
#+end_src

**** Disable Default Startup

Original startup is hideous...

#+begin_src emacs-lisp
(use-package "startup"
  :ensure nil
  :custom (inhibit-startup-screen t))
#+end_src

**** Modernize Selection Behaviour

Replaces active region just by typing text.

#+begin_src emacs-lisp
(use-package delsel
  :ensure nil
  :config (delete-selection-mode +1))
#+end_src

**** Disable Scroll-Bar

#+begin_src emacs-lisp
(use-package scroll-bar
  :ensure nil
  :custom
  ;; better scrolling experience
  (scroll-margin 0)
  (scroll-conservatively 101) ; > 100
  (scroll-preserve-screen-position t)
  (auto-window-vscroll nil)
  :config
  ;; Don't display the scroll bar in buffers
  (scroll-bar-mode -1))
#+end_src

**** Enable Column Numbers

#+begin_src emacs-lisp
(use-package simple
  :ensure nil
  :config
  (column-number-mode +1)
  (global-display-line-numbers-mode t))

  ;; DONT display line numbers in certain modes
  (dolist (mode '(term-mode-hook
                  shell-mode-hook
                  eshell-mode-hook
                  vterm-mode-hook))
  (add-hook mode (lambda () (display-line-numbers-mode 0))))
#+end_src

**** Split and Follow Windows

#+begin_src emacs-lisp
(use-package "window"
  :ensure nil
  :preface
  (defun zamlz/split-and-follow-horizontally ()
    "Split window below."
    (interactive)
    (split-window-below)
    (other-window 1))
  (defun zamlz/split-and-follow-vertically ()
    "Split window right."
    (interactive)
    (split-window-right)
    (other-window 1))
  :config
  (global-set-key (kbd "C-x 2") #'zamlz/split-and-follow-horizontally)
  (global-set-key (kbd "C-x 3") #'zamlz/split-and-follow-vertically))
#+end_src

**** Backup and Autosave Files

Emacs decides to save backup files and lockfiles within the same directory as the files we are editing. Thats just ugly when looking at the filesystem. This will fix that.

   #+begin_src emacs-lisp
   (use-package files
     :ensure nil
     :custom
     (create-lockfiles nil) ; don't create .# files (crashes 'npm start')
     (backup-directory-alist `(("." . "~/.config/emacs/backup"))))
   #+end_src

**** Eldoc Documentation

Slightly shorten the Eldoc display delay

#+begin_src emacs-lisp
(use-package eldoc
  :ensure nil
  :diminish eldoc-mode
  :custom
  (eldoc-idle-delay 0.4))
#+end_src

**** Mouse Wheel Scroll Speed

#+begin_src emacs-lisp
(use-package mwheel
  :ensure nil
  :custom
  (mouse-wheel-scroll-amount '(2 ((shift) . 1)))
  (mouse-wheel-progressive-speed nil))
#+end_src

**** Highlight Matching Parentheses

#+begin_src emacs-lisp
(use-package paren
  :ensure nil
  :custom (show-paren-delay 0)
  :config (show-paren-mode +1))
#+end_src

**** Auto-pairing Quotes and Parentheses

Super useful for auto-pairing certain characters. However we should make use of a hook to prevent it for left carrot bracket in org-mode buffers

#+begin_src emacs-lisp
(use-package elec-pair
  :ensure nil
  :hook
  (prog-mode . electric-pair-mode)
  ;; disable <> auto-pairing in org-mode buffers
  (org-mode  . (lambda ()
    (setq-local electric-pair-inhibit-predicate
                `(lambda (c)
                   (if (char-equal c ?<) t (,electric-pair-inhibit-predicate c)))))))
#+end_src

**** Clean Whitespace on Buffer Save

#+begin_src emacs-lisp
(use-package whitespace
  :ensure nil
  :hook (before-save . whitespace-cleanup))
#+end_src

**** Dump Custom-Set-Variables

#+begin_src emacs-lisp
(use-package cus-edit
  :ensure nil
  :custom (custom-file (concat user-emacs-directory "to-be-dumped.el")))
#+end_src

**** Easy PGP Assistant (EPA)

EPA is a built-in emacs package for interfacing with GnuPG.

#+begin_src emacs-lisp
(use-package epa-file
  :ensure nil
  :custom
  ;; Don't ask by default which key to use
  (epa-file-select-keys nil)
  ;; default to user mail address
  (epa-file-encrypt-to user-mail-address)
  ;; Set the pinentry mode to be loopback to gpg gets the password
  ;; through emacs instead of using pinentry.
  (epa-pinentry-mode 'loopback))
#+end_src

**** Auth Source Pass

The auth-source-pass package, formerly known as auth-password-store, integrates Emacs' auth-source library with password-store. The auth-source library is a way for Emacs to answer the old burning question “What are my user name and password?”. Password-store (or just pass) is a standard unix password manager following the Unix philosophy. More details can be found at [[https://github.com/DamienCassou/auth-source-pass][github:DamienCassou/auth-source-pass]].

#+begin_src emacs-lisp
(use-package auth-source-pass
  :ensure nil
  :init (auth-source-pass-enable))
#+end_src

**** Calc

#+begin_src emacs-lisp
(use-package calc
  :ensure nil
  :custom
  (calc-angle-mode 'rad)
  (calc-symbolic-mode t))
#+end_src

** keybinding Improvements

#+begin_src emacs-lisp
(require 'init-evil)
#+end_src

*** Reorganize This
**** General Keybinder

This is an interesting package. It basically lets me define my own keybinding space and configure it to run various commands as I see fit.

#+begin_src emacs-lisp
(use-package general
  :config
  (general-create-definer zamlz/leader-keys
    :keymaps '(normal insert visual emacs)
    :prefix "SPC"
    :global-prefix "C-SPC"))
#+end_src

**** Hydra

Hydra lets me also define functions that can very quickly let me do various tasks in quick succession. I will be using this a lot of =general= I imagine.

#+begin_src emacs-lisp
(use-package hydra)
#+end_src

**** Major Mode Hydra

#+begin_src emacs-lisp
(use-package major-mode-hydra
  :after hydra)
#+end_src

**** Which Key

Spawns a simple UI panel that shows available keybindings based on what keys I've pressed so far.

#+begin_src emacs-lisp
(use-package which-key
  :init (which-key-mode)
  :diminish which-key-mode
  :custom (which-key-idle-delay 1.0))
#+end_src

**** Misc Shortcuts

These are just random shorts to emacs built-in commands that I'd like access to as a keybinding.

#+begin_src emacs-lisp
(zamlz/leader-keys
 "t"  '(:ignore t :which-key "toggles")
 "tt" '(counsel-load-theme :which-key "choose theme"))
#+end_src

**** Text Size Scaling

Adds a =Hydra= function to =General= to control the size of the font face.

#+begin_src emacs-lisp
(defhydra hydra-text-scale (:timeout 4)
  "scale text"
  ("j" text-scale-increase "in")
  ("k" text-scale-decrease "out")
  ("f" nil "finished" :exit t))

;; Add hydra func to our personal keybindings
(zamlz/leader-keys
  "ts" '(hydra-text-scale/body :which-key "scale text"))
#+end_src


** Interface

#+begin_src emacs-lisp
(require 'init-ivy)
(require 'init-calfw)
#+end_src

*** Reorganize This
**** Startup Dashboard

The default landing page isn't quite nice. I originally had it configured to display the scratch page, but then I really wanted like a menu to quickly access my stuff.

#+begin_src emacs-lisp
;; Enable custom dashboard
(use-package dashboard
  :ensure t
  :custom
  ;; (dashboard-startup-banner "~/org/config/lib/emacs-themes/navi.png")
  ;; (dashboard-startup-banner "~/org/config/lib/emacs-themes/black-hole.png")
  (dashboard-startup-banner "~/org/config/lib/emacs-themes/name.txt")
  (dashboard-center-content t)
  (dashboard-set-heading-icons t)
  (dashboard-set-file-icons t)
  (dashboard-set-navigator t)
  (dashboard-set-init-info t)
  (initial-buffer-choice (lambda() (get-buffer "*dashboard*")))
  (dashboard-items '())
  :config
  (dashboard-modify-heading-icons '((bookmarks . "book")))
  (dashboard-setup-startup-hook))
#+end_src

**** Font Configuration

I have a lot of fonts commented out right now because I can't decide on which ones to keep lol.

#+begin_src emacs-lisp
(defun zamlz/set-font-faces ()
  ;; Set default face
  ;; (set-face-attribute 'default nil :font "xos4 Terminus" :height 110)
  ;; (set-face-attribute 'default nil :font "Fira Code" :height 100)
  ;; (set-face-attribute 'default nil :font "Dina" :height 100)
  (set-face-attribute 'default nil :font "Iosevka Term" :height 100)
  ;; (set-face-attribute 'default nil :font "Source Code Pro" :height 100)

  ;; Set the fixed pitch face
  ;; (set-face-attribute 'fixed-pitch nil :font "xos4 Terminus" :height 100)

  ;; Set the variable pitch face
  ;; (set-face-attribute 'variable-pitch nil :font "Fira Code" :height 100)
  )
#+end_src

**** Helpful Help Commands

[[https://github.com/Wilfred/helpful][Wilfred/helpful]] improves the documentation shown when running one of emacs's =describe-*= functions.

#+begin_src emacs-lisp
(use-package helpful
  :after counsel
  :custom
  ; This is only needed if I'm still using counsel
  (counsel-describe-function-function #'helpful-callable)
  (counsel-describe-variable-function #'helpful-variable)
  :bind
  ;; Note that the built-in `describe-function' includes both functions
  ;; and macros. `helpful-function' is functions only, so we provide
  ;; `helpful-callable' as a drop-in replacement.
  ([remap describe-function] . helpful-callable)
  ([remap describe-variable] . helpful-variable)
  ([remap describe-key]      . helpful-key)
  ([remap describe-command]  . helpful-command)
  ("C-c C-d"                 . helpful-at-point)
  ("C-h F"                   . helpful-function)
  )
#+end_src

**** Themes and Appearance
***** Modeline

To use =Doom-Modeline=, we need to have some custom icons installed. However, they must be manually installed via the following command (=M-x all-the-icons-install-fonts=)

#+begin_src emacs-lisp
(use-package all-the-icons)

(use-package doom-modeline
  :init (doom-modeline-mode 1)
  :custom ((doom-modeline-height 15)))
#+end_src

***** Solaire Mode

#+begin_src emacs-lisp
(use-package solaire-mode
  :custom
  (solaire-mode-auto-swap-bg t)
  :config (solaire-global-mode +1))
#+end_src

***** Color Theme

I have a couple themes here. Eventually I want to setup my own custom theme but for now this will have to do.

#+begin_src emacs-lisp
(use-package autothemer
  :ensure t)

(add-to-list 'custom-theme-load-path "~/org/config/lib/emacs-themes/")
;; (load-theme 'gruvbox-black t)

(use-package doom-themes
  ;; Solaire mode won't work unless its global mode is setup before the
  ;; =load-theme= function is called.
  :after (solaire-mode)
  :config
  ;; Global settings (defaults)
  (setq doom-themes-enable-bold t    ; if nil, bold is universally disabled
        doom-themes-enable-italic t) ; if nil, italics is universally disabled
  (load-theme 'doom-henna t)
  ;; Enable flashing mode-line on errors
  (doom-themes-visual-bell-config)
  ;; Enable custom neotree theme (all-the-icons must be installed!)
  (doom-themes-neotree-config)
  ;; or for treemacs users
  (setq doom-themes-treemacs-theme "doom-colors") ; use the colorful treemacs theme
  (doom-themes-treemacs-config)
  ;; Corrects (and improves) org-mode's native fontification.
  (doom-themes-org-config))

;; (use-package tron-legacy-theme
;;   :custom
;;   (tron-legacy-theme-vivid-cursor t)
;;   ;; :config (load-theme 'tron-legacy t)
;;   )

;; (use-package spacemacs-theme
;;   :defer t
;;   ;; :init (load-theme 'spacemacs-dark t)
;;   )

;; (use-package atom-one-dark-theme
;;   :config (load-theme 'atom-one-dark t))
#+end_src

***** Rainbow Delimiters

Normally I don't like rainbow delimiters but its actually pretty good on emacs. And you actually can't survive without it IMO.

#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :hook (prog-mode . rainbow-delimiters-mode))
#+end_src

***** Syntax Highlighting

#+begin_src emacs-lisp
(use-package highlight-numbers
  :hook (prog-mode . highlight-numbers-mode))

(use-package highlight-escape-sequences
  :hook (prog-mode . hes-mode))
#+end_src

***** Rainbow Mode

#+begin_src emacs-lisp
(use-package rainbow-mode
  :init (rainbow-mode))
#+end_src

***** Transparency

#+begin_src emacs-lisp
;; Set transparency of emacs
(defun zamlz/set-transparency (value)
  "Sets the transparency of the frame window. 0=transparent/100=opaque"
  (interactive "nTransparency Value 0 - 100 opaque:")
  (set-frame-parameter (selected-frame) 'alpha value))

;; Add the transparency function to my leader keys
(zamlz/leader-keys
  "tx" '(zamlz/set-transparency :which-key "Set transparency"))

;; Set the default transparency
(zamlz/set-transparency 100)
#+end_src

**** Info Colors

#+begin_src emacs-lisp
(use-package info-colors
  :hook
  (Info-selection . info-colors-fontify-node)
  (info-mode . mixed-pitch-mode))
#+end_src

**** Daemon and Client Hooks

#+begin_src emacs-lisp
(if (daemonp)
    (add-hook 'after-make-frame-functions
              (lambda (frame)
                (setq doom-modeline-icon t)
                (with-selected-frame frame
                  (zamlz/set-font-faces)
                  (zamlz/set-transparency 100))))
  (zamlz/set-font-faces))
#+end_src

**** Misc Packages
***** Vterm

Installs a better virtual terminal to use within emacs. I'm just playing around with this for now.

#+begin_src emacs-lisp
(use-package vterm
  :custom
  ;; (vterm-shell "/bin/fish")
  (vterm-ignore-blink-cursor nil)
  (vterm-buffer-name-string "vterm [%s]")
  (vterm-always-compile-module t)
  (vterm-kill-buffer-on-exit t)
  (vterm-max-scrollback 5000))

(zamlz/leader-keys
  "e" '(:ignore t :which-key "Exec Commands")
  "ee" '(vterm :which-key "Spawn vterm instance"))
#+end_src

***** PDF Tools

A nice standalone replacement for DocView.

#+begin_src emacs-lisp
(use-package pdf-tools
  :hook (pdf-tools-enabled . pdf-view-midnight-minor-mode)
  :custom
  (pdf-view-midnight-colors
   `(,(face-attribute 'term :foreground)
     . ,(face-attribute 'term-color-black :foreground)))
  :init (pdf-tools-install))
#+end_src

***** Mu4e

We make use of [[file:isync.org][Isync (mbsync)]] to clone a local copy of the IMAP to use with mu4e.

#+begin_src emacs-lisp
(use-package mu4e
  :ensure nil
  ;; :load-path "/usr/share/emacs/site-lisp/mu4e/"
  ;; :defer 20 ; Wait until 20 seconds after startup

  :config
  ;; This is set to 't' to avoid mail syncing issues when using mbsync
  (setq mu4e-change-filenames-when-moving t)

  ;; Refresh mail using isync every 10 minutes
  (setq mu4e-update-interval (* 10 60))
  (setq mu4e-get-mail-command "mbsync -a")
  (setq mu4e-maildir "~/.mail/samlesh@gmail.com/")

  (setq mu4e-sent-folder   "/[Gmail]/Sent Mail")
  (setq mu4e-trash-folder  "/[Gmail]/Trash")
  (setq mu4e-drafts-folder "/[Gmail]/Drafts")
  (setq mu4e-refile-folder "/[Gmail]/All Mail")

  (setq mu4e-maildir-shortcuts
    '((:maildir "/inbox"    :key ?i)
      (:maildir "/[Gmail]/Sent Mail" :key ?s)
      (:maildir "/[Gmail]/Trash"     :key ?t)
      (:maildir "/[Gmail]/Drafts"    :key ?d)
      (:maildir "/[Gmail]/All Mail"  :key ?a))))
#+end_src

** Integrated Development Environment
*** Utilities

#+begin_src emacs-lisp
(require 'init-git)
(require 'init-projectile)
(require 'init-company)
(require 'init-flycheck)
#+end_src

*** Languages

#+begin_src emacs-lisp
(require 'init-python)
(require 'init-julia)
(require 'init-ledger)
(require 'init-markdown)
#+end_src

** Org Mode Packages

#+begin_src emacs-lisp
(require 'init-org)
(require 'init-org-babel)
(require 'init-org-roam)
#+end_src

* Xresources Setup
:PROPERTIES:
:header-args:C: :tangle ~/.Xresources.d/emacs :mkdirp yes :comments no
:END:
The colorscheme below doesn't actually matter. Emacs will override it anyway when you load our actual colorscheme. *However*, Emacs does in fact load this before rendering the GUI Window where as our actual colorscheme is loaded after the GUI Window is drawn. /Therefore/, this simple setup prevents the blinding white flash from appearing at startup! *Secondly*, if the Emacs config is bricked for some reason, our barebones environment will still be in /dark/ mode.

#+begin_src C
Emacs.foreground: xforeground
Emacs.background: xbackground

Emacs.color0:  xcolor0
Emacs.color1:  xcolor1
Emacs.color2:  xcolor2
Emacs.color3:  xcolor3
Emacs.color4:  xcolor4
Emacs.color5:  xcolor5
Emacs.color6:  xcolor6
Emacs.color7:  xcolor7
Emacs.color8:  xcolor8
Emacs.color9:  xcolor9
Emacs.color10: xcolor10
Emacs.color11: xcolor11
Emacs.color12: xcolor12
Emacs.color13: xcolor13
Emacs.color14: xcolor14
Emacs.color15: xcolor15
#+end_src

* Portage Setup
** Pacakges
:PROPERTIES:
:header-args:conf: :tangle ~/.config/portage/sets/apps-emacs :mkdirp yes :comments both
:END:
We need to ensure that emacs gets installed via the package set. This code block actually isn't needed because we need to already have emacs installed before we could even possibly have tangled this! None the less, we have it here for the sake of the package sets.

#+begin_src conf
app-editors/emacs
#+end_src

Currently the utilites needed for =mu4e= have to installed via the distro's pacakge manager so lets ensure that portage pulls the needed files in here.

#+begin_src conf
net-mail/isync
net-mail/mu
#+end_src

** USE Flags
:PROPERTIES:
:header-args:conf: :tangle ~/.config/portage/package.use/apps-emacs :mkdirp yes :comments both
:END:

#+begin_src conf
app-editors/emacs cairo dbus dynamic-loading gui gif gpm harfbuzz imagemagick
app-editors/emacs jpeg png sound svg threads xft tiff athena Xaw3d -gtk -motif
app-editors/emacs xwidgets libxml2
#+end_src

#+begin_src conf
net-mail/mu emacs
#+end_src

#+begin_src conf
# required by app-editors/emacs-27.1-r2::gentoo
# required by @world (argument)
=app-emacs/emacs-common-gentoo-1.6-r4 gui
=app-emacs/emacs-common-1.8 gui
#+end_src
