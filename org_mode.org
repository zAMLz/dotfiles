#+TITLE: Org-mode
#+AUTHOR: Amlesh Sivanantham (zamlz)
#+ROAM_KEY: https://orgmode.org/
#+ROAM_ALIAS:
#+ROAM_TAGS: CONFIG SOFTWARE EMACS
#+CREATED: [2021-03-27 Sat 00:18]
#+LAST_MODIFIED: [2021-05-12 Wed 13:06:57]
#+STATUP: content

* Your Life in Plain Text
#+DOWNLOADED: screenshot @ 2021-03-31 18:49:35
[[file:data/org_mode_logo.png]]

Org-mode is a powerful plain text markup language for [[file:emacs.org][GNU/Emacs]]. You can use it for taking notes, generating to-do-lists, creating research papers, running code, you name it!

* Useful Resources
- [[https://orgmode.org/manual/index.html][The Official Org Manual]]
- [[https://blog.jethro.dev/posts/org_mode_workflow_preview/][Jethro Kuan's Org-mode Workflow]]
- [[https://www.reddit.com/r/orgmode/comments/i2d75e/tracking_my_diet_with_emacs_and_orgmode/][Reddit: Tracking my diet with Emacs and Org-mode]]

* GTD System
I'll use this section here to document how I use the [[file:../notes/gtd_framework.org][GTD Framework]] with org-mode.

* Configuration
:PROPERTIES:
:header-args:emacs-lisp: :tangle ~/.config/emacs/lisp/init-org.el :comments both :mkdirp yes
:END:
** Basic Configuration

I should probably split this bloody mess up.

#+begin_src emacs-lisp
(use-package org
  :ensure org-plus-contrib
  :hook
  (org-mode    . org-indent-mode)
  (org-mode    . variable-pitch-mode)
  (org-mode    . zamlz/org-mode-setup)
  (org-mode    . zamlz/org-level-face-setup)
  (org-mode    . zamlz/org-fixed-pitch-setup)
  (before-save . zamlz/update-org-modified-property-hook)
  :bind (:map org-mode-map
              ("C-M-h" . org-previous-link)
              ("C-M-l" . org-next-link))
  :custom
  ;; Setup directories
  (org-directory "~/org/gtd/")
  (org-agenda-files (list org-directory))
  ;; Add some nice visuals changes
  ;;(org-ellipsis " ▾")
  (org-ellipsis "  ")
  ;; This is so that the imenu displays all levels in ivy
  (org-imenu-depth 6)
  ;; dont use C-c C-o for opening links REEEEEEEEEEEE
  (org-return-follows-link t)
  ;; Some todo/logging changes
  (org-enforce-todo-dependencies t)
  (org-log-done 'note)
  (org-log-into-drawer t)
  (org-treat-S-cursor-todo-selection-as-state-change nil)
  ;; Setup org capture mode
  ;; Setup refiling
  (org-log-refile t)
  (org-refile-use-outline-path 'file)
  (org-outline-path-complete-in-steps nil)
  (org-refile-allow-creating-parent-nodes nil)
  ;; TODO: need to figure out how to setup project refile targets
  (org-refile-targets
   `((,(concat org-directory "/tasks.org") :maxlevel . 5)))
  ;; Setup archive location
  (org-archive-location (concat org-directory "/archive.org::"))
  ;; ensure that refiling saves buffers
  (advice-add 'org-refile :after 'org-save-all-org-buffers)
  ;; Inline Image improvements
  (org-startup-with-inline-images t)
  (org-image-actual-width 300)
  ;; Align org tags
  (org-tags-column (* -1 (- zamlz/default-screen-width 10)))
  (org-agenda-tags-column (* -1 zamlz/default-screen-width )))
#+end_src

** Mode Startup Hooks

These are some hooks that we would like to run when =org-mode= is started.

#+begin_src emacs-lisp
(defun zamlz/org-mode-setup ()
  (visual-line-mode +1)
  (setq evil-auto-indent nil)
  (setq fill-column 10000))
#+end_src

** Better Font Faces Hook

We can configure =org-mode= font faces via a seperate function that we define. We need to add it as a hook afterwards however which is why this section appears before the basic configuration.

#+begin_src emacs-lisp
(defun zamlz/org-level-face-setup ()
  (interactive)
  ;; hide #+TITLE:
  (setq org-hidden-keywords '(title))
  (setq org-fontify-whole-heading-line t)
  (setq org-fontify-done-headline t)
  (setq org-fontify-quote-and-verse-blocks t)
  ;; set basic title font
  (set-face-attribute 'org-level-8 nil :weight 'normal :inherit 'default :slant 'italic)
  ;; Low levels are unimportant => no scaling
  (set-face-attribute 'org-level-7 nil :inherit 'org-level-8)
  (set-face-attribute 'org-level-6 nil :inherit 'org-level-8)
  (set-face-attribute 'org-level-5 nil :inherit 'org-level-8)
  (set-face-attribute 'org-level-4 nil :inherit 'org-level-8)
  ;; Top ones get scaled the same as in LaTeX (\large, \Large, \LARGE)
  (set-face-attribute 'org-level-3 nil :inherit 'org-level-8 :height 1.2) ;\large
  (set-face-attribute 'org-level-2 nil :inherit 'org-level-8 :height 1.44) ;\Large
  (set-face-attribute 'org-level-1 nil :inherit 'org-level-8 :height 1.728) ;\LARGE
  ;; Only use the first 4 styles and do not cycle.
  (setq org-cycle-level-faces nil)
  (setq org-n-level-faces 4)
  ;; Document Title, (\huge)
  (set-face-attribute 'org-document-title nil
                      :slant 'normal
                      :weight 'normal
                      :height 3.074
                      :foreground 'unspecified
                      :inherit 'org-level-8))
#+end_src

Thanks to [[https://protesilaos.com/codelog/2020-07-17-emacs-mixed-fonts-org/][this blog post]] from Protesliaos, I was able to properly configure variable pitched fonts in these =org-mode= buffers.

#+begin_src emacs-lisp
(defun zamlz/org-fixed-pitch-setup ()
  (interactive)
  (dolist (face '(org-block
                  org-block-begin-line
                  org-code
                  org-drawer
                  org-special-keyword
                  org-property-value
                  org-document-info-keyword
                  org-meta-line
                  org-table
                  org-verbatim))
    (set-face-attribute `,face nil :inherit 'fixed-pitch))
  (set-face-attribute 'org-block-end-line nil :inherit 'org-block-begin-line))
#+end_src

** Task Categories

#+begin_src emacs-lisp
(setq org-todo-keywords
      (quote ((sequence "TODO(t)"
                        "SOMEDAY(s)"
                        "MEETING(m)"
                        "WAITING(w)"
                        "|"
                        "DONE(d)"
                        "CANCELLED(c)"))))
#+end_src

** Task Colors

This new version of the task colors pulls in existing colors from preexisting faces and uses the doom-theme built in functionality to darken and recolor them selevtively. This one configuration will now color all my tasks categories how I want it to be depending on the doom-theme.

#+begin_src emacs-lisp
(defun zamlz/configure-org-todo-keyword-faces ()
  (interactive)
  (message "Configure org todo keyword faces")
  (let
      ((black   (doom-color 'bg))
       (grey    (doom-color 'base1))
       (red     (doom-color 'red))
       (orange  (doom-color 'orange))
       (green   (doom-color 'green))
       (yellow  (doom-color 'yellow))
       (blue    (doom-color 'blue))
       (violet  (doom-color 'violet))
       (cyan    (doom-color 'cyan))
       (white   (doom-color 'white)))
    (setq org-todo-keyword-faces
          `(("TODO"      . (:weight bold :foreground ,black  :background ,red))
            ("MEETING"   . (:weight bold :foreground ,black  :background ,violet))
            ("DONE"      . (:weight bold :foreground ,black  :background ,green))
            ("WAITING"   . (:weight bold :foreground ,cyan   :background ,grey))
            ("SOMEDAY"   . (:weight bold :foreground ,violet :background ,grey))
            ("CANCELLED" . (:weight bold :foreground ,red    :background ,grey))
            ))))
#+end_src

The tasks colors function needs to be called. Unfortunately, there is an issue where its not really set when we're running it via a daemon.

#+begin_src emacs-lisp
(zamlz/configure-org-todo-keyword-faces)
#+end_src

** Center Org buffers

Center the org buffers and remove line numbers to reduce visual clutter.

#+begin_src emacs-lisp
(defun zamlz/org-mode-visual-fill ()
  (setq visual-fill-column-width zamlz/default-screen-width
        ;; visual-fill-column-extra-text-width (0 . 1000)
        visual-fill-column-center-text t)
  (visual-fill-column-mode 1))

 (use-package visual-fill-column
   :hook (org-mode . zamlz/org-mode-visual-fill))
#+end_src

** Org Keybindings

Just quick access to my most used org-mode functions via the [[file:general_el.org][General Leader Keybindings Definer]].

#+begin_src emacs-lisp
(require 'init-general)
(zamlz/leader-keys
  "o"  '(:ignore t :which-key "Org Mode")
  "oa" '(org-agenda :which-key "org-agenda")
  "oc" '(org-capture :which-key "org-capture")
  "ol" '(org-store-link :which-key "org-store-link")
  "oi" '((lambda () (interactive)
           (org-tags-view t "INBOX")
           (message "Opened:  %s" (buffer-name)))
         :which-key "Inbox")
  "og" '((lambda () (interactive)
           (find-file (concat org-directory "/tasks.org"))
           (message "Opened:  %s" (buffer-name)))
         :which-key "GOTO tasks.org"))
#+end_src

** Org Capture Templates
*** DOCT: Declarative Org Capture Templates

DOCT is a cool package that lets me build =org-capture= templates with ease. It's really way more readable than the original syntax.

#+begin_src emacs-lisp
(use-package doct
  :ensure t
  ;;recommended: defer until calling doct
  :commands (doct))
#+end_src

*** Template Definitions

Org mode template definitions in the doct format

#+begin_src emacs-lisp
(setq org-capture-templates
      (doct '(("Agenda Capture for GTD Tasks" :keys "a"
               :file "tasks.org"
               :headline "Inbox"
               :type entry
               :prepend t
               :template ("* %{todo-state} %^{Description} :INBOX:"
                          ":PROPERTIES:"
                          ":Created: %U"
                          ":END:"
                          "%?")
               :children (("Todo Task" :keys "t"
                           :todo-state "TODO")
                          ("Routine/Habit" :keys "r"
                           :todo-state "ROUTINE")
                          ("Project Group" :keys "p"
                           :todo-state "PROJECT")
                          ("Someday/Maybe" :keys "s"
                           :todo-state "SOMEDAY")
                          ("Meeting/Appointment" :keys "m"
                           :todo-state "MEETING"))))))
#+end_src

** Org Make TOC

Dynamically make table of contents in org files

#+begin_src emacs-lisp
(use-package org-make-toc
  :after org
  :hook (org-mode . org-make-toc-mode))
#+end_src

** LaTeX Inline Preview

Pretty straightforward except that you need to have [[file:latex.org][LaTeX]] installed and also =dvipng= as well. We'll add =dvipng= to this file's package set.

/Need to figure out how to configure the size of the generated latex image./

#+begin_src emacs-lisp
  (setq org-startup-with-latex-preview t)
  (setq org-highlight-latex-and-related '(native script entities))
  (with-eval-after-load 'org
    (setq org-format-latex-options (plist-put org-format-latex-options :scale 2.0))
    (setq org-preview-latex-image-directory  "/tmp/ltximg/")
    (add-to-list 'org-src-block-faces '("latex" (:inherit default :extend t))))
#+end_src

** Org Latex FragTog

[[https://github.com/io12/org-fragtog][github:io12/org-fragtog]] basically toggles latex fragements when you hover over them. Here is some inline latex to test it with: \(\int_0^1\frac{x^2}{3}\). And now where is some more but on a seperate line:

\[\sqrt{\beta^2+5}-\sum_{\phi=1}^\infty \frac{x^\phi-1}{\Gamma(a)}\]

#+begin_src emacs-lisp
(use-package org-fragtog
  :hook (org-mode . org-fragtog-mode))
#+end_src

** Habit Tracking

Enables habit tracking in =org-mode= via =org-habit=.

#+begin_src emacs-lisp
(use-package org-habit
  :ensure nil
  :after org
  :init (add-to-list 'org-modules 'org-habit t)
  :custom
  (org-habit-graph-column 40)
  (org-habit-preceding-days 28)
  (org-habit-following-days 7)
  (org-habit-show-habits-only-for-today t)
  (org-habit-show-all-today t)
  (org-habit-show-done-always-green t))
#+end_src

** Org Download

This package should let me quickly download images from web browsers and have images in my clipboard and paste them into my org files.

#+begin_src emacs-lisp
(use-package org-download
  :custom
  (org-download-image-dir "./data")
  (org-download-heading-lvl nil)
  (org-download-method 'directory))
#+end_src

** Org Last Modified Timestamp

The following allows any =#+LAST_MODIFIED= headers to be updated on file-save.

#+begin_src emacs-lisp
(defun zamlz/update-org-modified-property-hook ()
  "If an org file contains a '#+LAST_MODIFIED' property,
  update it to contain the current date/time"
  (when (eq major-mode 'org-mode)
    (interactive)
    (save-excursion
      (widen)
      (goto-char (point-min))
      (when (re-search-forward "^#\\+LAST_MODIFIED:" (point-max) t)
        (progn
          (delete-region (point) (save-excursion (move-end-of-line 1) (point)))
          (insert (format-time-string " [%Y-%m-%d %a %H:%M:%S]") ))))))
#+end_src

** Custom Link Types

The variable =org-link-abbrev-alist= provides us a powerful way of defining our own link types to be used within org-mode. Let's define one for Google Maps below. Now to use it, we simply need to type =[[gmap:UCSC, Santa Cruz][UCSC]]= and we'll have a link open up to google maps!

#+begin_src emacs-lisp
(setq org-link-abbrev-alist
      '(("gmap" . "http://maps.google.com/maps?q=%s")))
#+end_src

** Package Provider

#+begin_src emacs-lisp
(provide 'init-org)
#+end_src
